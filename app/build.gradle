apply plugin: 'com.android.application'
//apply from: 'tinker-support.gradle'//(热更新相关)
def keystorePropertiesFile = rootProject.file("keystore.properties")//打包文件配置
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    def version = rootProject.ext.versions
    compileSdkVersion version.compileSdkVersion
    buildToolsVersion version.buildToolsVersion
    defaultConfig {
        applicationId version.applicationId
        minSdkVersion version.minSdkVersion
        targetSdkVersion version.targetSdkVersion
        versionCode version.versionCode
        versionName version.versionName
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "armeabi", "x86_64", "mips", 'mips64'
        }
        javaCompileOptions {   //解决AndroidStudio 3.0的问题
            annotationProcessorOptions { includeCompileClasspath = true }
        }
        multiDexKeepProguard file('tinker_multidexkeep.pro')
        manifestPlaceholders = [CHANNEL: "AA000",]//可用于三方sdk使用，例如极光推送的参数，可在AndroidManifest中使用，最后获取到这个参数
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable true//是否允许调试
            minifyEnabled true//是否开启代码混淆
            zipAlignEnabled true//是否优化apk文件，将apk文件中未压缩的数据在4个字节边界上对齐
            shrinkResources true//是否去除无用资源，任何在编译过程中没有用到的资源或者代码都会被删除，可以有效减小apk体积
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false //true--所有正式版构建执行规则生成崩溃的lint检查，如果有崩溃问题将停止构建
        abortOnError false  //true--错误发生后停止gradle构建
        disable 'MissingTranslation' //关闭指定问题检查
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    dexOptions {//解决编译内存溢出问题
        dexInProcess true
        preDexLibraries true
        javaMaxHeapSize "8g"
    }
    repositories {
        flatDir {
            dirs 'libs','../frame/libs'
        }
    }
}

dependencies {
    def dependencies = rootProject.ext.deps
    //walle读取渠道号
    implementation dependencies.walle
    annotationProcessor dependencies.glide.compiler
    annotationProcessor dependencies.butterknife.compiler
    //下载上传工具类
    annotationProcessor dependencies.aria.aria_compiler
    implementation project(':frame')
}
